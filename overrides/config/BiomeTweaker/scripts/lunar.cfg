Tweaker.setScriptStage("BIOME_REGISTRY")
Tweaker.createBiome("lunar_mare")
Tweaker.createBiome("lunar_midlands")
Tweaker.createBiome("lunar_highlands")
Tweaker.createBiome("lunar_canyons")

lunar_mare = forBiomes("biometweaker:lunar_mare")
lunar_midlands = forBiomes("biometweaker:lunar_midlands")
lunar_highlands = forBiomes("biometweaker:lunar_highlands")
lunar_biomes = forBiomes("biometweaker:lunar_mare","biometweaker:lunar_midlands","biometweaker:lunar_highlands")

anorthosite_regolith = forBlock("contenttweaker:anorthosite_regolith")
basalt_regolith = forBlock("contenttweaker:basalt_regolith")


basalt = forBlock("undergroundbiomes:igneous_stone")
basalt.setProperty("type", "basalt")

basalt_gravel = forBlock("undergroundbiomes:igneous_gravel")
basalt_gravel.setProperty("type", "basalt")

andesite = forBlock("undergroundbiomes:igneous_stone")
andesite.setProperty("type", "andesite")


lunar_biomes.addDicTypes("WASTELAND")
lunar_biomes.addDicTypes("COLD")

lunar_biomes.set("enableRain",false)
lunar_biomes.set("enableSnow",false)
lunar_biomes.set("enableRain",false)
lunar_biomes.set("enableRain",false)



Tweaker.setScriptStage("FINISHED_LOAD")
lunar_mare.set("name","Lunar Mare")
lunar_mare.set("height",0)
lunar_mare.set("heightVariation",0)
lunar_mare.set("topBlock",basalt_regolith)
lunar_mare.set("fillerBlock",basalt)


lunar_midlands.set("name","Lunar Midlands")
lunar_midlands.set("height",0.5)
lunar_midlands.set("heightVariation",0.3)
lunar_midlands.set("topBlock",anorthosite_regolith)
lunar_midlands.set("fillerBlock",andesite)


lunar_highlands.set("name","Lunar Highlands")
lunar_highlands.set("height",1.5)
lunar_highlands.set("heightVariation",0.45)
lunar_highlands.set("topBlock",anorthosite_regolith)
lunar_highlands.set("fillerBlock",andesite)




Tweaker.setPlacementStage("POST_POPULATE")

removeWater = newBlockReplacement()
removeWater.set("ignoreMeta", true)
removeWater.set("block", "minecraft:air")
lunar_biomes.registerGenBlockRep("minecraft:water", removeWater)

removeLava = newBlockReplacement()
removeLava.set("ignoreMeta", true)
removeLava.set("block", "minecraft:air")
lunar_biomes.registerGenBlockRep("minecraft:lava", removeLava)






